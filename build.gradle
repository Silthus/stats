buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'java'
    id 'jacoco'
    id "io.freefair.lombok" version "5.3.0"
    id 'io.ebean' version '12.6.5'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    depends = ['ebean-wrapper']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    spigot()
    maven { url 'https://jitpack.io' }
}

dependencies {

    compileOnly spigot(mcVersion)

    //Add dependencies here
    implementation 'net.silthus:ebean-wrapper:2.9.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.1'

    implementation 'io.ebean:ebean:12.6.5'
    implementation 'io.ebean:ebean-querybean:12.7.1'
    annotationProcessor 'io.ebean:querybean-generator:12.6.7'

    //Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation mockBukkit()
    testImplementation 'org.assertj:assertj-core:3.+'

    testImplementation 'io.ebean:ebean-test:12.8.0'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.1'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('com.fasterxml.jackson.core::'))
    }
    relocate 'com.fasterxml.jackson', "shadow.${pluginName}.jackson"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

ebean {
    debugLevel = 1
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'